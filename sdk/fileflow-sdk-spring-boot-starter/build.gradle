plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.ryuqq.fileflow'
version = '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    // Core SDK
    api project(':sdk:fileflow-sdk-core')

    // Spring Boot
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'

    // Annotation Processor for configuration metadata
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'FileFlow SDK Spring Boot Starter'
                description = 'Spring Boot auto-configuration for FileFlow SDK'
                url = 'https://github.com/ryu-qqq/FileFlow'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'ryuqq'
                        name = 'Sangwon Ryu'
                    }
                }
            }
        }
    }
}
