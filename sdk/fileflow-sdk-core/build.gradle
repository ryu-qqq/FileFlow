plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.ryuqq.fileflow'
version = '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    // JSON 처리
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Logging
    implementation 'org.slf4j:slf4j-api'

    // Integration Test - MockWebServer
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'FileFlow SDK Core'
                description = 'Core SDK library for FileFlow REST API integration'
                url = 'https://github.com/ryu-qqq/FileFlow'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'ryuqq'
                        name = 'Sangwon Ryu'
                    }
                }
            }
        }
    }
}
